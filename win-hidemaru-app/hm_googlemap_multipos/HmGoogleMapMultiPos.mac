/* 
 * HmGoogleMapMultiPos v1.01
 * Copyright (c) 2018 Akitsugu Komiyama
 * under the MIT License
 */

#V8 = loaddll(hidemarudir + @"\hmV8.dll" );
if (!#V8) {
    message "hmV8が導入されていないようです!";
    endmacro;
}

#r = dllfuncw(#V8, "DoString", R"V8(

try {
    // このマクロのファイルのフルパス
    let currentmacrofilename = hm.Macro.Var["currentmacrofilename"];

    // このマクロファイル自体を対象にマクロを実行している。 → 意味がないのでエラー＆誘導
    if (currentmacrofilename == hm.Macro.Var["filename"] ) {
        throw "GoogleMapで表示すべき文字列リストが含まれている秀丸タブを\n" +
              "アクティブにした状態でマクロを実行して下さい。\n\n" +
              "このマクロ自体を対象にはできません。\n";
    }

    // 対象のテキスト。選択ありなら選択、なしなら全体
    let target_text = hm.Edit.SelectedText || hm.Edit.TotalText;

    // テキスト内容が空 → エラー＆警告
    if (!target_text) {
        throw "GoogleMapで表示すべき対象の文字列が存在しません。";
    }

    // 駄目文字が入っている場合用の対策としてHTMLエンコード。
    host.lib(clr, "System.Web");
    let encoded_text = clr.System.Web.HttpUtility.HtmlEncode(target_text);

    // テンプレートファイルをテキストとして取得。
    let tmpl_filename = clr.System.IO.Path.ChangeExtension(currentmacrofilename, ".tmpl");
    let sr = new clr.System.IO.StreamReader(tmpl_filename, clr.System.Text.Encoding.UTF8);
    let tmpl_text = sr.ReadToEnd();
    sr.Close();

    // 最終的に表示するHTML。テンプレートテキストの"%location_list%"の部分を、場所の文字列リストで置換え
    let result_html = tmpl_text.replace(/%location_list%/g, encoded_text);

    // ファイルとして出力。
    let result_filename = clr.System.IO.Path.ChangeExtension(currentmacrofilename, ".html");
    let sw = new clr.System.IO.StreamWriter(result_filename, false, clr.System.Text.Encoding.UTF8);
    sw.Write(result_html);
    sw.Close();

    // Chromeで表示。引数部分空白入ってるかもなので" "で囲む。
    clr.System.Diagnostics.Process.Start("chrome", '"'+result_filename+'"');

// なんかエラー
} catch(e) {

    // ダイアログでエラー内容表示
    hm.Macro.Var["$HmGoogleMapMultiPosError"] = e.toString();
    hm.Macro.Eval(f => {/* message $HmGoogleMapMultiPosError; */});
}

)V8"
);

freedll(#V8);
