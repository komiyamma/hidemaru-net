/*
 * HmEnglishTextWordCounter ver 1.001
 * Copyright (C) 2021 Akitsugu Komiyama
 * under the MIT License
 */

#HMNET = loaddll( hidemarudir + @"\hm.NET.dll");
if (!#HMNET) {
    message "hm.NET.dll が見つかりません。";
}

#_ = dllfuncw( #HMNET, "SetDetachMethod", currentmacrodirectory + @"\HmEnglishTextWordCounter.dll", "HmEnglishTextWordCounter.HmEnglishTextWordCounnter", "DetachWordCounter" );
#is_count_mode = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmEnglishTextWordCounter.dll", "HmEnglishTextWordCounter.HmEnglishTextWordCounnter", "IsCountMode");

// 実行する度に、「表示」と「やめ」を切り替える
if (#is_count_mode) {
    title 0, 1;
    #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmEnglishTextWordCounter.dll", "HmEnglishTextWordCounter.HmEnglishTextWordCounnter", "StopWordCounter");
    #OUTPUT = loaddll("HmOutputPane.dll");
    #ret = dllfunc(#OUTPUT, "Output",hidemaruhandle(0),"カウントモード終了\r\n");
    freedll(#OUTPUT);


} else {
    #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmEnglishTextWordCounter.dll", "HmEnglishTextWordCounter.HmEnglishTextWordCounnter", "StartWordCounter", R"MACRO(
        $word_counter = "{WORD_COUNT}"; // HmEnglishTextWordCounter はここにWord数を入れるだろう。

        if (selecting) {
            title "選択範囲の単語数:" + $word_counter, 1;   //スターテスバーに表示
            title -1, 1;    //ステータスバー表示を維持
        } else {
            title "全体の単語数:" + $word_counter, 1;   //スターテスバーに表示
            title -1, 1;    //ステータスバー表示を維持
        }

        )MACRO"

        , "\n\t .,:;\"'?!"  // これらの文字(制御文字)を単語と単語の区切り目とみなす
    );
}




