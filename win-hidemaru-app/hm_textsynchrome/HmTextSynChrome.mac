/* 
 * Copyright (c) 2017-2019 Akitsugu Komiyama
 * under the Apache License Version 2.0
 */

:
$argOption1 = getarg(0);

$curOption1 = "--lang=ja"; // デフォルトは「日本語」への翻訳


// 引数で指定が無いなら、メニューで選ぶ
if ($argOption1 == "") {
    $menuOptions[0] = "--lang=ja"   ; $menuTitle[0] = "日本語";
    $menuOptions[1] = "--lang=en"   ; $menuTitle[1] = "英語";
    $menuOptions[2] = "--lang=zh-CN"; $menuTitle[2] = "中国語(簡体字)";
    $menuOptions[3] = "--lang=zh-TW"; $menuTitle[3] = "中国語(繁体字)";
    $menuOptions[4] = "--lang=ko"   ; $menuTitle[4] = "韓国語";
    $menuOptions[5] = "--lang=fr"   ; $menuTitle[5] = "フランス語";
    $menuOptions[6] = "--lang=de"   ; $menuTitle[6] = "ドイツ語";
    $menuOptions[7] = "--lang=it"   ; $menuTitle[7] = "イタリア語";
    $menuOptions[8] = "--lang=es"   ; $menuTitle[8] = "スペイン語";
    $menuOptions[9] = "--lang=pt"   ; $menuTitle[9] = "ポルトガル語";
    $menuOptions[10]= "--lang=ru"   ; $menuTitle[10]= "ロシア語";

    #menu_length = 0;
    while(true) {
        if ($menuTitle[#menu_length] == "") {
            break;
        }
        #menu_length = #menu_length + 1;
    }
    mousemenuarray $menuTitle, #menu_length;
    #select_index = result - 1;
    if (0 <= #select_index && #select_index < #menu_length) {
        $curOption1 = $menuOptions[#select_index];
    } else {
        endmacro;
    }

// 引数で指定があるなら、直接代入
} else {
    $curOption1 = $argOption1;
}


newfile;
title "HmTextSynChrome「"+$curOption1+"」タブ" , 3;
title -1, 3;


#HMNET = loaddll( hidemarudir + @"\hm.NET.dll");

if (#HMNET) {
    #_ = dllfuncw(#HMNET, "SetDetachMethod", currentmacrodirectory + @"\HmTextSynChrome.dll", "HmTextSynChrome", "OnDetachMethod");
    #_ = dllfuncw(#HMNET, "CallMethod", currentmacrodirectory + @"\HmTextSynChrome.dll", "HmTextSynChrome", "Show", hidemaruhandle(0), $curOption1 );
} else {
    message("Hm.NET.dllを読み込むことが出来ません");
}

