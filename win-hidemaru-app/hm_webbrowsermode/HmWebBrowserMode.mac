/*
 * Copyright (c) 2017-2019 Akitsugu Komiyama
 * under the Apache License Version 2.0
 */
 
disablebreak;

AnalyzeArgument:
    $url = getarg(0);
    // 引数があるなら、そこにきっと飛びたいのだろう。
    if ($url != "") {
        // ローカルにファイルが存在するなら、バイナリで開いて警告を回避しつつ、そのままHmWebBrowserへと流していく。
        if (existfile($url)) {
            openfile "/h " + $url, binary;
        // ローカルに存在しないなら、URLと自動的にみなす。
        } else {
            newfile;
            disabledraw;
            $html = R"(<meta http-equiv="refresh" content="0; url=)" + $url + R"(">)";
            insert $html;
            clearupdated;
            title $url, 3;
            title -1, 3;
        }

    // 引数はなしで、「.mac」を対象にこのマクロを有効にするとダウンロードしようとするので「は？」となって鬱陶しすぎる。
    } else if (filetype == ".mac") {
		enablebreak;
        endmacro;
    }

HmWebBrowser:
    #HMNET = loaddll( hidemarudir + @"\hm.NET.dll" );

    if (#HMNET) {

        call SetWebBrowserIEVersion;

        #_ = dllfuncw(#HMNET, "SetDetachMethod", currentmacrodirectory + @"\HmWebBrowserMode.dll", "HmWebBrowserMode", "OnDetachMethod");

        // マクロを手動実行ならスイッチ。拡張子等の条件はなし。即ち、「実行されてないなら、実行」「実行されてるなら、閉じる」
        if (event == 0) {
            #WebBrowserIsNull = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmWebBrowserMode.dll", "HmWebBrowserMode", "IsNull" );
            if (#WebBrowserIsNull) {
                #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmWebBrowserMode.dll", "HmWebBrowserMode", "Create" );
            } else {
                #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmWebBrowserMode.dll", "HmWebBrowserMode", "Destroy" );
            }

        // ファイル読込時。拡張子の条件がマッチするなら。
        } else if (event == 1) {
            #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmWebBrowserMode.dll", "HmWebBrowserMode", "Create" );
        }

    } else {
        message("hm.NET.dllを読み込むことが出来ません");
    }


enablebreak;
endmacro;


SetWebBrowserIEVersion:
    // １回だけ実行した後は、以後ずっと有効なので、再びこの return を有効にしておくと良い。
    // マクロの度に毎回レジストリを変更するのは無駄。

    // return;

    // 11001 はIE11のEdgeレンダリングを表す。WebBrowserコンポーネントは、これが今後も当面はMAXの数値になると思われる。
    runex R"(REG ADD "HKCU\SOFTWARE\Microsoft\Internet Explorer\Main\FeatureControl\FEATURE_BROWSER_EMULATION" /v Hidemaru.exe /f /t REG_DWORD /d 11001)"
        , 1             //sync    0:async 1:sync
        , 0, ""         //stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
        , 0, ""         //stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace 7:output frame 8:add output frame
        , 0, ""         //stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace 7:output frame 8:add output frame
        , 0, ""         //folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folderxxxxx
        , 2             //show    0:auto 1:show 2:hide
        , 1             //nodraw  0:draw 1:no draw
        , 0             //unicode 0:ansi 2:unicode 6:UTF8
        ;

    return;


