/*
 * HmGoogleLangTranslatorJS.mac v1.0.0.1
 * Copyright (c) 2019 Akitsugu Komiyama
 * under the MIT License
 */

// 環境変数から、Google Apps Script の翻訳APIへのURLアドレスを取得。
$url = getenv("GOOGLE_SCRIPT_TRANSLATION_API");

// 環境変数ではなく、直接ファイルに書いてしまうなら以下を書き換えて、コメントアウトも外す
// $url = "https://script.google.com/macros/s/********************************************/exec";

if (filename == currentmacrofilename) {
    // このマクロ自身に対して実行するのは、
    // マクロのソース中に文字列を挿入してしまうため
    // 意図せずマクロを破壊してしまう可能性が高い。
    // 
    // ほぼ確実に意図しない操作とみなして、止める
    message("HmGoogleLangTranslator.mac 自身を対象にはできません。");
    endmacro;
}

$source=getarg(0);
$target=getarg(1);

if (!#DLL) { #DLL = loaddll(hidemarudir + @"\hmV8.dll"); }
if (!#DLL) { #DLL = loaddll(hidemarudir + @"\hmJS.dll"); }
#r = dllfuncw( #DLL, "DoString", R"JS(

currentmacrodirectory = hm.Macro.Var("currentmacrodirectory");
var lib = host.lib(currentmacrodirectory + "/HmGoogleLangTranslate.dll");

var text   = hm.Edit.SelectedText || hm.Edit.TotalText

var url    = hm.Macro.Var("$url");
var source = hm.Macro.Var("$source");
var target = hm.Macro.Var("$target");

var Translator = lib.HmGoogleLangTranslate.HmGoogleLangTranslate;
var answer = Translator.Translate(text, url, source, target);
hm.Macro.Var('$answer', answer);

)JS"
);

// 翻訳した文字列は$answerという変数に入ってるので、
// 好きにして… 💔
escape;
moveto selendx, selendy; // 選択してたら解除しつつも選択範囲の最後尾に
insert "\n" + $answer;
